import Tensorflow.Tensor
import Tensorflow.Types
import Tensorflow.Operations
import Tensorflow.CWrappers.Helpers
import Tensorflow.GeneratedOps

# TODO this type parameter is temporary, due to lack of interfaces
class DropoutLayer layerT:
    name :: Text
    rate :: Real
    parentLayer :: layerT # :: Layer # TODO no interfaces

    def toText:
        self.name + self.shape.toText

    def toJSON:
        name = self.name
        shape = self.shape
        parents = [self.parentLayer]
        props = JSON.empty . insert "rate" self.rate
        type = "Dropout"
        JSON.empty . insert "name" name . insert "shape" shape . insert "parents" parents . insert "type" type . insert "properties" props

    # shape :: List Int
    def shape:
        self.parentLayer.shape

    # forward :: Operation a -> Operation a
    def forward inp:
        myin = self.parentLayer.forward inp

        shapeConst = Operations.constFromList Int32Type self.shape
        uniform = Operations.randomUniform shapeConst FloatType

        keepProb = Operations.constFromScalar FloatType (1.0 - self.rate)
        shiftedUniform = keepProb + uniform
        mask = floorGen "" shiftedUniform FloatType

        out = (myin * mask) / keepProb
        out

    # eval :: Operation a -> Operation a
    def eval inp:
        myin = self.parentLayer.eval inp
        myin

    # trainableVariables :: List Variable a
    def trainableVariables:
        self.parentLayer.trainableVariables

    def outputType:
        self.parentLayer.outputType

class Dropout:
    Dropout

    # dropout :: Real -> Layer -> Layer
    def create rate parent:
        name = getNextName "Dropout"
        d = DropoutLayer name rate parent
        d
