import Tensorflow.Tensor
import Tensorflow.Types
import Tensorflow.Operations
import Tensorflow.CWrappers.Helpers
import Tensorflow.GeneratedOps

# TODO this type parameter is temporary, due to lack of interfaces
class DropoutLayer layerT:
    name :: Text
    rate :: Real
    parentLayer :: layerT # :: Layer # TODO no interfaces

    def toText:
        self.name + self.shape.toText

    def toJSON:
        self.toText.toJSON

    # shape :: List Int
    def shape:
        self.parentLayer.shape

    # forward :: Operation a -> Operation a
    def forward inp:
        myin = self.parentLayer.forward inp

        shapeConst = constFromList Int32Type self.shape
        uniform = randomUniform shapeConst FloatType

        keepProb = constFromScalar FloatType (1.0 - self.rate)
        shiftedUniform = keepProb + uniform
        mask = floorGen "" shiftedUniform FloatType

        out = (myin * mask) / keepProb
        out

    # eval :: Operation a -> Operation a
    def eval inp:
        myin = self.parentLayer.eval inp
        myin

    # trainableVariables :: List Variable a
    def trainableVariables:
        self.parentLayer.trainableVariables

    def outputType:
        self.parentLayer.outputType

# dropout :: Real -> Layer -> Layer
def dropout rate parent:
    name = getNextName "Dropout"
    d = DropoutLayer name rate parent
    d