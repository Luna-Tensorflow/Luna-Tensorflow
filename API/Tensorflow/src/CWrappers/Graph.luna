import Std.Foreign
import Std.Foreign.C.Value

import Tensorflow.CWrappers.Tensor
import Tensorflow.CWrappers.State

import Tensorflow.Operations
import Tensorflow.Types
import Tensorflow.Graph

class GraphWrapper:
    ptr :: ManagedPointer None

    def toCArg:
        self.ptr.toCArg

    def eval outputCount state:
        evalSubsts outputCount [] state

    def evalSubsts outputCount substitutions state:
        count = outputCount
        phCount = substitutions . length

        evalGraphOp = lookupSymbol "TFL" "eval_graph_with_placeholders"
        releaseMethod = lookupSymbol "TFL" "release"

        (names, substs) = unzip substitutions

        subsNameArr = ManagedPointer CString . mallocElems phCount
        indexed = 0 . upto phCount . zip names
        indexed . each (idx, elem):
            subsNameArr . moveElems idx . write (CString . fromText elem)


        substsArr = ManagedPointer (Pointer None) . mallocElems phCount
        indexed = 0 . upto phCount . zip substs
        indexed . each (idx, elem):
            substsArr . moveElems idx . write (elem.ptr.ptr)

        returnedPtr = evalGraphOp.call (Pointer (Pointer None)) [self.toCArg,
                subsNameArr.toCArg, substsArr.toCArg, (CInt64.fromInt phCount).toCArg, state.wrapper.toCArg]

        returnedArr = Array (Pointer None) . make (1+count) returnedPtr

        statewrapper = StateWrapper (ManagedPointer None . fromPointer releaseMethod (returnedArr . readAt 0))

        managed = (returnedArr . toList . drop 1) . each (x: ManagedPointer None . fromPointer releaseMethod x)
        retwrappers = managed . each (ptr: TensorWrapper ptr)

        freePointer = lookupSymbol "TFL" "free_pointer"
        freePointer.call None [returnedPtr.toCArg]

        (statewrapper, retwrappers)

def makeGraphWrapperFromOutputList outputList:
    count = outputList . length

    makeGraphFnc = lookupSymbol "TFL" "make_graph_from_outputs"
    releaseMethod = lookupSymbol "TFL" "release"

    carr = ManagedPointer (Pointer None) . mallocElems count
    indexed = 0 . upto count . zip outputList
    indexed . each (idx, elem):
        carr . moveElems idx . write (elem.ptr.ptr)

    returnedPtr = makeGraphFnc.call (Pointer None) [carr.toCArg, (CInt64.fromInt count).toCArg]
    managedGraph = ManagedPointer None . fromPointer releaseMethod returnedPtr

    GraphWrapper managedGraph
