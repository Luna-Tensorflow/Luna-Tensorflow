import Std.Foreign
import Std.Foreign.C.Value

import Tensorflow.Operations
import Tensorflow.Tensor
import Tensorflow.Types

def unzip listpair: case listpair of
    Empty: (Empty, Empty)
    Prepend (x,y) tail: case unzip tail of
        (tailx, taily): ((Prepend x tailx), (Prepend y taily))

class GraphWrapper:
    ptr :: ManagedPointer None

    def toCArg:
        self.ptr.toCArg

    def eval outputCount:
        evalSubsts outputCount []

    def evalSubsts outputCount substitutions:
        count = outputCount
        phCount = substitutions . length

        evalGraphOp = lookupSymbol "TFL" "eval_graph_with_placeholders_float"
        releaseMethod = lookupSymbol "TFL" "release"

        (names, substs) = unzip substitutions

        subsNameArr = ManagedPointer CString . mallocElems phCount
        indexed = 0 . upto phCount . zip names
        indexed . each (idx, elem):
            subsNameArr . moveElems idx . write (CString . fromText elem)


        substsArr = ManagedPointer (Pointer None) . mallocElems phCount
        indexed = 0 . upto phCount . zip substs
        indexed . each (idx, elem):
            substsArr . moveElems idx . write (elem.ptr.ptr)

        returnedPtr = evalGraphOp.call (Pointer (Pointer None)) [self.toCArg,
                subsNameArr.toCArg, substsArr.toCArg, (CInt64.fromInt phCount).toCArg]

        returnedArr = Array (Pointer None) . make count returnedPtr

        retval = returnedArr . toList . each (tensorFromPtr releaseMethod FloatType)
        returnedArr . free
        retval

def makeGraphWrapperFromOutputList outputList:
    count = outputList . length

    makeGraphFnc = lookupSymbol "TFL" "make_graph_from_outputs_float"
    releaseMethod = lookupSymbol "TFL" "release"

    carr = ManagedPointer (Pointer None) . mallocElems count
    indexed = 0 . upto count . zip outputList
    indexed . each (idx, elem):
        carr . moveElems idx . write (elem.ptr.ptr)

    returnedPtr = makeGraphFnc.call (Pointer None) [carr.toCArg, (CInt64.fromInt count).toCArg]
    managedGraph = ManagedPointer None . fromPointer releaseMethod returnedPtr
    GraphWrapper managedGraph