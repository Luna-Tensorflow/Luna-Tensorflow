import Std.Foreign
import Std.Foreign.C.Value
import Tensorflow.Types

class TensorWrapper a:
    ptr :: ManagedPointer None
    typetag :: a

    def toCArg:
        self.ptr.toCArg

    def numDims:
        numDimsFun = lookupSymbol "TFL" "get_tensor_num_dims"

        res = numDimsFun.call CInt64 [self.toCArg]

        res.toInt

    def dims:
        n = self.numDims

        dimsFun = lookupSymbol "TFL" "get_tensor_dims"
        freeFun = lookupSymbol "TFL" "free_pointer"

        dimsPtr = dimsFun.call (Pointer CInt64) [self.toCArg]
        dimsArr = Array CInt64 . make n dimsPtr
        cdims = dimsArr.toList
        dims = cdims.each (i: i.toInt)
        freeFun.call None [dimsPtr.toCArg]

        dims

    def at indexes:
        atFun = lookupSymbol "TFL" ("get_tensor_value_at_" + self.typetag.typename)
        cindexes = indexes.map CInt64.fromInt

        cindexesArr = Array CInt64 . fromList cindexes

        res = atFun.call (self.typetag.ctype) [self.toCArg, cindexesArr . toCArg, CInt64.fromInt cindexes.length . toCArg]
        ret = self.typetag.fromC res
        self.typetag.cleanupC res

        cindexesArr.free

        ret

    def atIndex index:
        atIndexFun = lookupSymbol "TFL" ("get_tensor_value_at_index_" + self.typetag.typename)
        res = atIndexFun.call (self.typetag.ctype) [self.toCArg, CInt64.fromInt index . toCArg]
        ret = self.typetag.fromC res
        self.typetag.cleanupC res
        ret

    def flatLength:
        lenFun = lookupSymbol "TFL" "get_tensor_flatlist_length"
        res = lenFun.call CInt64 [self.toCArg]
        res.toInt

    def toFlatList:
        len = self.flatLength

        flatListFun = lookupSymbol "TFL" ("tensor_to_flatlist_" + self.typetag.typename)
        freeFun = lookupSymbol "TFL" "free_pointer"

        ptr = flatListFun.call (Pointer (self.typetag.ctype)) [self.toCArg]
        arr = Array (self.typetag.ctype) . make len ptr
        clist = arr.toList
        list = clist.each (self.typetag.fromC)
        clist.each (self.typetag.cleanupC)
        freeFun.call None [ptr.toCArg]

        list

def createTensorWrapper typetag dims values:
    expectedValuesCount = dims.foldLeft 1 (x: a: a * x)
    if (values.length == expectedValuesCount).not then throw "values must be exactly of the size needed to fill a tensor with dimensions dims" else None

    cvalues = values.each typetag.toC
    cvaluesArray = (Array typetag.ctype) . fromList cvalues

    cdims = dims.map CInt64.fromInt
    cdimsArray = (Array CInt64) . fromList cdims

    makeTensor = lookupSymbol "TFL" "make_tensor"
    releaseMethod = lookupSymbol "TFL" "release"

    tensor = makeTensor.call (Pointer None) [cvaluesArray.toCArg, CInt32.fromInt typetag.num . toCArg, cdimsArray.toCArg, CInt64.fromInt cdims.length . toCArg]

    cvalues.each typetag.cleanupLuna
    cvaluesArray.free
    cdimsArray.free

    managedTensor = ManagedPointer None . fromPointer releaseMethod tensor
    TensorWrapper managedTensor typetag

def createRandomTensorWrapper typetag dims minValue maxValue:
    minCVal = typetag.toC minValue
    maxCVal = typetag.toC maxValue

    cdims = dims.map CInt64.fromInt
    cdimsArray = (Array CInt64) . fromList cdims

    makeRandomTensor = lookupSymbol "TFL" ("make_random_tensor_" + typetag.typename)
    releaseMethod = lookupSymbol "TFL" "release"

    tensor = makeRandomTensor.call (Pointer None) [cdimsArray.toCArg, CInt64.fromInt cdims.length . toCArg, minCVal . toCArg, maxCVal . toCArg]

    cdimsArray.free

    managedTensor = ManagedPointer None . fromPointer releaseMethod tensor
    TensorWrapper managedTensor typetag

def createConstTensorWrapper typetag dims value:
    cVal = typetag.toC value

    cdims = dims.map CInt64.fromInt
    cdimsArray = (Array CInt64) . fromList cdims

    makeConstTensor = lookupSymbol "TFL" ("make_const_tensor_" + typetag.typename)
    releaseMethod = lookupSymbol "TFL" "release"

    tensor = makeConstTensor.call (Pointer None) [cdimsArray.toCArg, CInt64.fromInt cdims.length . toCArg, cVal . toCArg]

    cdimsArray.free

    managedTensor = ManagedPointer None . fromPointer releaseMethod tensor
    TensorWrapper managedTensor typetag


