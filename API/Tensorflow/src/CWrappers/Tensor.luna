import Std.Foreign
import Std.Foreign.C.Value
import Tensorflow.Types

class TensorWrapper a:
    ptr :: ManagedPointer None
    typetag :: a

    def toCArg:
        self.ptr.toCArg

    def length:
        lengthFun = lookupSymbol "TFL" ("get_tensor_length_" + self.typetag.typename)
        res = lengthFun.call CInt64 [self.toCArg]
        res.toInt

    def at indexes:
        atFun = lookupSymbol "TFL" ("get_tensor_value_at_" + self.typetag.typename)
        cindexes = indexes.map CInt64.fromInt
        res = atFun.call (self.typetag.ctype) [self.toCArg, Array CInt64 . fromList cindexes . toCArg, CInt64.fromInt cindexes.length . toCArg]
        self.typetag.fromC res

def create1dTensorWrapper typetag values:
    cvalues = values.map (typetag.toC)
    makeTensor = lookupSymbol "TFL" ("make_tensor_" + typetag.typename)
    releaseMethod = lookupSymbol "TFL" "release"
    carray = (Array (typetag.ctype)) . fromList cvalues 
    tensor = makeTensor.call (Pointer None) [carray.toCArg, CInt64.fromInt cvalues.length . toCArg]
    managedTensor = ManagedPointer None . fromPointer releaseMethod tensor
    TensorWrapper managedTensor typetag
