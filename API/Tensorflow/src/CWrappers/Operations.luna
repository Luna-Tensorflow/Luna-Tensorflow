import Std.Foreign
import Std.Foreign.C.Value
import Tensorflow.Tensor
import Tensorflow.Operations
import Tensorflow.CWrappers.Tensor
import Tensorflow.Types
import Tensorflow.Patches

import Tensorflow.CWrappers.Helpers

def unzip listpair: case listpair of
    Empty: (Empty, Empty)
    Prepend (x,y) tail: case unzip tail of
        (tailx, taily): ((Prepend x tailx), (Prepend y taily))


class OutputWrapper:
    ptr :: ManagedPointer None

    def toCArg:
        self.ptr.toCArg

    def eval:
        tensorPtr = callHandlingError "eval_op" (Pointer None) [self.toCArg]
        managedTensorPtr = ManagedPointer None . fromPointer releaseMethod tensorPtr
        managedTensorPtr

    def opName:
        cstr = callHandlingError "get_operation_name" CString [self.toCArg]
        name = cstr.toText
        name

# TODO likely not used, remove
def batchEval list:
    batchEvalSubstitutions list []

def batchEvalSubstitutions list substitutions:
    list' = list.each id
    count = list' . length
    phCount = substitutions . length

    carr = Pointer (Pointer None) . mallocElems count
    indexed = 0 . upto (count - 1) . zip list'
    indexed . each (idx, elem):
        ptr = elem.ptr.ptr
        carr . moveElems idx . write (PointerVal None ptr)

    (names, substs) = unzip substitutions
    substs' = substs.each id

    subsNameArr = Pointer CString . mallocElems phCount
    indexed = 0 . upto (phCount - 1) . zip names
    indexed . each (idx, elem):
        subsNameArr . moveElems idx . write (CString . fromText elem)


    substsArr = Pointer (Pointer None) . mallocElems phCount
    indexed = 0 . upto (phCount - 1) . zip substs'
    indexed . each (idx, elem):
        ptr = elem.ptr.ptr
        substsArr . moveElems idx . write (PointerVal None ptr)

    returnedPtr = callHandlingError "batch_eval_op_placeholders" (Pointer (Pointer None)) [carr.toCArg, (CInt64.fromInt count).toCArg,
            subsNameArr.toCArg, substsArr.toCArg, (CInt64.fromInt phCount).toCArg]

    returnedArr = Array (Pointer None) . make count returnedPtr

    retval = returnedArr . toList . each (tensorFromPtr FloatType) # TODO: different types support

    callHandlingError "free_pointer" None [returnedPtr.toCArg]

    0.upto (phCount - 1) . each (idx: subsNameArr . moveElems idx . read . free)
    subsNameArr.free
    substsArr.free
    carr.free

    retval

def makeConstWrapper tensor name:
    tensor' = tensor
    out = CString.with name nameCStr:
            callHandlingError "make_op_const" (Pointer None) [nameCStr.toCArg, tensor'.ptr.toCArg]
    managedOut = ManagedPointer None . fromPointer releaseMethod out
    OutputWrapper managedOut

def makeConstFromRealWrapper real typetag name:
    double = CDouble.fromReal real
    out = CString.with name nameCStr:
            callHandlingError "make_op_const_from_real" (Pointer None) [nameCStr.toCArg, CInt32.fromInt typetag.num . toCArg, double.toCArg]
    managedOut = ManagedPointer None . fromPointer releaseMethod out
    OutputWrapper managedOut

def makePlaceholderWrapper typetag name:
    out = CString.with name nameCStr:
            callHandlingError "make_op_placeholder" (Pointer None) [nameCStr.toCArg, CInt32.fromInt typetag.num . toCArg]

    managedOut = ManagedPointer None . fromPointer releaseMethod out

    OutputWrapper managedOut

def makeBinaryOpWrapper name a b:
    None # TODO: add assert a.typetag == b.typetag
    a' = a
    b' = b
    out = CString.with name nameCStr:
            callHandlingError "make_op_binary" (Pointer None) [nameCStr.toCArg, a'.ptr.toCArg, b'.ptr.toCArg]

    managedOut = ManagedPointer None . fromPointer releaseMethod out

    OutputWrapper managedOut

def makeUnaryOpWrapper name a:
    a' = a

    out = CString.with name nameCStr:
            callHandlingError "make_op_unary" (Pointer None) [nameCStr.toCArg, a'.ptr.toCArg]

    managedOut = ManagedPointer None . fromPointer releaseMethod out

    OutputWrapper managedOut

def makePartialDerivativeWrapper a b:
    a' = a
    b' = b
    out = callHandlingError "make_op_partial_derivative" (Pointer None) [a'.ptr.toCArg, b'.ptr.toCArg]
    managedOut = ManagedPointer None . fromPointer releaseMethod out
    OutputWrapper managedOut

def outputWrapperFromPtr ptr:
    managedOutputPtr = ManagedPointer None . fromPointer releaseMethod ptr
    OutputWrapper managedOutputPtr

# TODO remove outputTypetags
def makeOutputWrappers name inputs noutputs attrList chosenName:
    inputs' = inputs.each id
    attrList' = attrList

    count = inputs'.length
    inputsCArr = Pointer (Pointer None) . mallocElems count
    "indexed = 0 . upto (count - 1) . zip inputs'"
    indexed = Patches.zip (0 . upto (count - 1)) inputs'
    indexed . each (idx, input):
        ptr = input.ptr.ptr
        inputsCArr . moveElems idx . write (PointerVal None ptr)

    outsPtr = CString.with name nameCStr: CString.with chosenName chosenNameCStr:
            callHandlingError "make_op" (Pointer (Pointer None)) [nameCStr.toCArg, inputsCArr.toCArg, CInt32.fromInt inputs'.length . toCArg, CInt32.fromInt noutputs . toCArg, attrList'.toCArg, chosenNameCStr.toCArg]

    outsArr = Array (Pointer None) . make noutputs outsPtr
    outs = outsArr.toList.each output: outputWrapperFromPtr output

    inputsCArr.free

    callHandlingError "free_pointer" None [outsPtr.toCArg]

    outs
