import Std.Foreign
import Std.Foreign.C.Value
import Tensorflow.Tensor
import Tensorflow.Operations
import Tensorflow.CWrappers.Tensor

def unzip listpair: case listpair of
    Empty: (Empty, Empty)
    Prepend (x,y) tail: case unzip tail of
        (tailx, taily): ((Prepend x tailx), (Prepend y taily))


class OperationWrapper a:
    ptr :: ManagedPointer None
    typetag :: a

    def toCArg:
        self.ptr.toCArg

    def eval:
        evalOp = lookupSymbol "TFL" ("eval_op_" + self.typetag.typename)
        releaseMethod = lookupSymbol "TFL" "release"
        tensorPtr = evalOp.call (Pointer None) [self.toCArg]
        tensorFromPtr self.typetag tensorPtr

def batchEval list:
    batchEvalSubstitutions list []

def batchEvalSubstitutions list substitutions:
    count = list . length
    phCount = substitutions . length

    batchEvalOp = lookupSymbol "TFL" "batch_eval_op_placeholders_float"
    releaseMethod = lookupSymbol "TFL" "release"

    carr = ManagedPointer (Pointer None) . mallocElems count
    indexed = 0 . upto count . zip list
    indexed . each (idx, elem):
        carr . moveElems idx . write (elem.ptr.ptr)

    (names, substs) = unzip substitutions

    subsNameArr = ManagedPointer CString . mallocElems phCount
    indexed = 0 . upto phCount . zip names
    indexed . each (idx, elem):
        subsNameArr . moveElems idx . write (CString . fromText elem)


    substsArr = ManagedPointer (Pointer None) . mallocElems phCount
    indexed = 0 . upto phCount . zip substs
    indexed . each (idx, elem):
        substsArr . moveElems idx . write (elem.ptr.ptr)

    returnedPtr = batchEvalOp.call (Pointer (Pointer None)) [carr.toCArg, (CInt64.fromInt count).toCArg,
            subsNameArr.toCArg, substsArr.toCArg, (CInt64.fromInt phCount).toCArg]

    returnedArr = Array (Pointer None) . make count returnedPtr

    retval = returnedArr . toList . each (tensorFromPtr releaseMethod)
    returnedArr . free
    retval

def makeConstWrapper tensor:
    makeOp = lookupSymbol "TFL" ("make_op_const_" + tensor.wrapper.typetag.typename)
    releaseMethod = lookupSymbol "TFL" "release"
    op = makeOp.call (Pointer None) [tensor.ptr.toCArg]
    managedOp = ManagedPointer None . fromPointer releaseMethod op
    OperationWrapper managedOp tensor.wrapper.typetag

def makePlaceholderWrapper typetag name:
    makeOp = lookupSymbol "TFL" ("make_op_placeholder_" + typetag.typename)
    releaseMethod = lookupSymbol "TFL" "release"
    op = makeOp.call (Pointer None) [(CString.fromText name).toCArg]
    managedOp = ManagedPointer None . fromPointer releaseMethod op
    OperationWrapper managedOp typetag

def makeBinaryOpWrapper name a b:
    None # TODO: add assert a.typetag == b.typetag
    makeOp = lookupSymbol "TFL" ("make_op_binary_" + a.typetag.typename)
    releaseMethod = lookupSymbol "TFL" "release"
    op = makeOp.call (Pointer None) [(CString.fromText name).toCArg, a.ptr.toCArg, b.ptr.toCArg]
    managedOp = ManagedPointer None . fromPointer releaseMethod op
    OperationWrapper managedOp a.typetag

def makeUnaryOpWrapper name a:
    makeOp = lookupSymbol "TFL" ("make_op_unary_" + a.typetag.typename)
    releaseMethod = lookupSymbol "TFL" "release"
    op = makeOp.call (Pointer None) [(CString.fromText name).toCArg, a.ptr.toCArg]
    managedOp = ManagedPointer None . fromPointer releaseMethod op
    OperationWrapper managedOp a.typetag

def makePartialDerivativeWrapper a b:
    makeOp = lookupSymbol "TFL" ("make_op_partial_derivative_" + a.typetag.typename)
    releaseMethod = lookupSymbol "TFL" "release"
    op = makeOp.call (Pointer None) [a.ptr.toCArg, b.ptr.toCArg]
    managedOp = ManagedPointer None . fromPointer releaseMethod op
    OperationWrapper managedOp a.typetag

def operationWrapperFromPtr ptr typetag:
    releaseMethod = lookupSymbol "TFL" "release"
    managedOperationPtr = ManagedPointer None . fromPointer releaseMethod ptr
    OperationWrapper managedOperationPtr typetag
