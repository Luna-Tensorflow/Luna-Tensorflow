import Tensorflow.Graph
import Tensorflow.Operations

class Model layerT:
      evalgraph :: TFGraph
      traingraph :: TFGraph
      vars :: List TFOutput
      state :: State
      outLayer :: layerT
      # outLayer is only used for toJSON so might be removed if we change that

      def toJSON:
          self.outLayer.toJSON

      # evaluate :: Tensor a -> Tensor b
      def evaluate x:
          (res, state') = self.evalgraph.evalSubsts self.state [("x", x)]
          res

      # train :: List Tensor a -> List Tensor b -> Model # TODO also consider real time visualization of loss in GUI
      def train xs ys:
          0 # TODO we'd prefer to use batched fold that runs in C++ directly
          state' =  self.traingraph.foldEval ["x","y"] (xs.zipWith (x: y: [x,y]) ys) self.state # (xs.zip ys).foldLeft self.state ((x,y): state: (self.traingraph.evalSubsts state [("x", x), ("y", y)]).second)
          Model self.evalgraph self.traingraph self.vars state' self.outLayer

      # saveWeights :: Text -> ()
      def saveWeights filename:
          StateOps.saveVariables filename self.vars self.state

      # loadWeights :: Text -> Model
      def loadWeights filename:
          state' = StateOps.loadVariables filename self.vars StateOps.makeEmpty
          Model self.evalgraph self.traingraph self.vars state' self.outLayer

class Models:
    Models

    def make input output optimizer loss:
        inputPlaceholder = Operations.makePlaceholder input.typetag "x"
        yPredTrain = output.forward inputPlaceholder
        yPredEval = output.eval inputPlaceholder
        outType = yPredEval.typetag

        yTruePlaceholder = Operations.makePlaceholder outType "y"

        evalgraph = TFGraphMaker.makeFromOutput yPredEval

        variables = output.trainableVariables
        traingraph = optimizer.makeOptimizingGraph yTruePlaceholder yPredTrain loss variables

        initialState = StateOps.makeEmpty # TODO state initialization by layers

        vars = output.trainableVariables

        Model evalgraph traingraph vars initialState output
